#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{fatpack_info} = {
    v => 1.1,
    summary => 'Given a fatpacked script, show various info',
    args => {
        script => {
            schema => 'str*',
            req => 1,
            pos => 0,
            cmdline_src => 'stdin_or_file',
        },
        list_modules => {
            schema => ['bool' => is=>1],
            cmdline_aliases => {l=>{}},
            tags => ['category:action'],
        },
    },
};
sub fatpack_info {
    my %args = @_;

    my $fatpack_sig_found;

    my $in_fatpacked;
    my $module;
    my $end_marker;
    my %modules;
    my $tot_lines;
    my $tot_bytes;
    for (split /^/, $args{script}) {
        if ($in_fatpacked) {
            if (/^\Q$end_marker\E$/) {
                $in_fatpacked = 0;
                next;
            } else {
                s/^  //;
                $tot_lines++;
                $tot_bytes += length;
                $modules{$module}{lines}++;
                $modules{$module}{bytes} += length;
                next;
            }
        } else {
            if (/^\$fatpacked\{"(.+?)"\} = .+<<'(.+)';$/) {
                $in_fatpacked++;
                $module = $1;
                $end_marker = $2;
                next;
            }
            if (/^my \%fatpacked;/) {
                $fatpack_sig_found++;
                next;
            }
        }
    }

    if (!$fatpack_sig_found && !%modules) {
        return [412, "File content does not seem like a fatpacked script"];
    }

    my $res;

    if ($args{list_modules}) {
        $res = [];
        for (sort keys %modules) {
            s/\.pm$//; s!/!::!g;
            push @$res, $_;
        }
    } else {
        $res = {
            file_size => length($args{script}),
            modules => \%modules,
            tot_fatpacked_lines => $tot_lines,
            tot_fatpacked_bytes => $tot_bytes,
        };
    };

    [200, "OK", $res];
}

Perinci::CmdLine::Any->new(
    url => '/main/fatpack_info',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

 % fatpack-info /tmp/script.fatpack

=cut
